apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def propertiesFile = rootProject.file("local.properties")
Properties properties = new Properties()
properties.load(propertiesFile.newDataInputStream())
def baseUrl = properties.getProperty("BASE_URL")
android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId 'me.sweetll.tucao.new'
        minSdkVersion 19
        targetSdkVersion versions.targetSdk
        versionCode 118
        versionName '1.1.6'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
        buildConfigField("String","BASE_URL",baseUrl)
    }
    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    if (project.hasProperty("devBuild")) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }
}


kapt {
    useBuildCache = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testImplementation 'junit:junit:4.12'
    /* DialogPlus */
    implementation project(':dialogplus')
    /* GSYVideoPlayer */
    implementation project(':gsyVideoPlayer')
    /* TakePhoto */
    implementation project(':takephoto_library')
    implementation 'com.android.support:multidex:1.0.3'
    /* Support Library */
    implementation "com.android.support:support-v4:${versions.supportLibrary}"
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${versions.supportLibrary}"
    implementation "com.android.support:cardview-v7:${versions.supportLibrary}"
    implementation "com.android.support:gridlayout-v7:${versions.supportLibrary}"
    implementation "com.android.support:design:${versions.supportLibrary}"
    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    kapt "com.android.databinding:compiler:${versions.gradle}"
    /* Anko */
    implementation "org.jetbrains.anko:anko-commons:${versions.anko}"
    /* RxJava2 */
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    /* RxKotlin */
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    /* RxBinding */
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    /* RxLifecycle */
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.2.1'
    /* RxPermissions */
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5'
    /* Retrofit */
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.5.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    /* Dagger2 */
    implementation 'com.google.dagger:dagger:2.14.1'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    /* Glide */
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:annotations:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    /* RecyclerView Adapter */
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    /* Jsoup */
    implementation 'org.jsoup:jsoup:1.11.2'
    /* Crash */
    implementation 'me.drakeet.library:crashwoodpecker:2.1.1'
    /* Umeng */
    implementation 'com.umeng.analytics:analytics:6.1.3'
    /* EventBus */
    implementation 'org.greenrobot:eventbus:3.1.1'
    /* Banner */
    implementation 'com.bigkoo:convenientbanner:2.0.5'
    /* About */
    implementation 'com.github.medyo:android-about-page:1.2'
    /* Leak Canary */
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.2'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.2'
//    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.2'
    kapt 'com.github.Raizlabs.DBFlow:dbflow-processor:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-core:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:4.0.5'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.8.0")
}

repositories {
    mavenCentral()
}


